# Maintainer:  Christian Babeux <christian.babeux@0x80.ca>
# Contributor: Tianjiao Yin <ytj000(at)gmail(dot)(see)(oh)(em)>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=gcc45
pkgver=4.5.4
pkgrel=2
pkgdesc="The GNU Compiler Collection (4.5.x)"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'custom')
url="http://gcc.gnu.org"
depends=('glibc' 'binutils' 'gmp' 'mpfr' 'libmpc' 'ppl' 'isl' 'cloog' 'elfutils')
makedepends=('flex' 'bison' 'setconf')
options=('!libtool' '!buildflags')
provides=('gcc=4.5')
source=("http://www.mirrorservice.org/sites/sourceware.org/pub/gcc/releases/gcc-$pkgver/gcc-$pkgver.tar.bz2"
        "gcc-hash-style-both.patch"
	"gcc_pure64.patch")

sha1sums=("cb692e6ddd1ca41f654e2ff24b1b57f09f40e211"
          "c635c0cb4e5839f4b9f5d8317ce79668bfea2b26"
          "541a01c2843deddc775ff2256345c58f0bdf298e")

prepare() {
  cd "$srcdir/gcc-$pkgver"

  # Do not install libiberty
  sed -i -e 's:install_to_$(INSTALL_DEST) ::' libiberty/Makefile.in

  # Do not run fixincludes
  sed -i -e 's:\./fixinc\.sh:-c true:' gcc/Makefile.in

  patch -Np0 -i "$srcdir/gcc-hash-style-both.patch"

  if [[ "$CARCH" == "x86_64" ]]; then
    patch -Np1 -i "$srcdir/gcc_pure64.patch"
  fi

  echo "$pkgver" > gcc/BASE-VER

  setconf gcc/configure BUILD_INFO ''
}

build() {
  cd "$srcdir/gcc-$pkgver"

  rm -rf build
  mkdir build
  cd build

  ../configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --program-suffix=-4.5 \
    --enable-shared \
    --enable-languages=c,c++,fortran,objc,obj-c++ \
    --enable-__cxa_atexit \
    --disable-libstdcxx-pch \
    --disable-multilib \
    --disable-libgomp \
    --disable-libmudflap \
    --disable-libssp \
    --enable-clocale=gnu \
    --with-tune=generic \
    --with-cloog \
    --with-ppl \
    --with-system-zlib

  make
}

package() {
  cd "$srcdir/gcc-$pkgver/build"

  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share/man/man7"
  rm -rf "$pkgdir/usr/share/locale"
  mv "$pkgdir"/usr/lib/lib* \
     "$pkgdir/usr/lib/gcc/$CHOST/$pkgver/"
  # Install Runtime Library Exception
  install -Dm644 ../COPYING.RUNTIME \
    "$pkgdir/usr/share/licenses/$pkgname/RUNTIME.LIBRARY.EXCEPTION" 
  # Create links for gcc-4.5 build environment (useful for CUDA)
  mkdir -p "$pkgdir/opt/gcc-4.5"
  ln -s /usr/bin/gcc-4.5 "$pkgdir/opt/gcc-4.5/gcc"
  ln -s /usr/bin/g++-4.5 "$pkgdir/opt/gcc-4.5/g++"
}
