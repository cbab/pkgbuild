# Maintainer: Christian Babeux <christian.babeux@0x80.ca>

pkgname=lttngtop-git
pkgver=20130124
pkgrel=1
pkgdesc="A top-like, ncurses-based utility to analyze trace information produced by LTTng"
arch=('i686' 'x86_64')
url="http://lttng.org/"
license=('GPL2')
makedepends=('git')
provides=('lttngtop')
conflicts=('lttngtop')
depends=('glib2>=2.22.0' 'ncurses' 'popt>=1.13' 'babeltrace')

_gitroot="git://git.lttng.org/lttngtop.git"
_gitname="lttngtop"

build()
{
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./bootstrap
  ./configure --prefix=/usr
  make
}

package()
{
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}
