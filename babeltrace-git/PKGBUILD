# Maintainer: Christian Babeux <christian.babeux@0x80.ca>

pkgname=babeltrace-git
pkgver=20130124
pkgrel=1
pkgdesc="command-line trace reader for LTTng"
arch=('i686' 'x86_64')
url="http://lttng.org/"
license=('GPL' 'MIT')
makedepends=('git')
depends=('popt' 'flex>=2.5.35' 'util-linux' 'glib2>=2.22.0')
conflicts=('babeltrace')
install='babeltrace-git.install'

_gitroot="git://git.efficios.com/babeltrace.git"
_gitname="babeltrace"

build()
{
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./bootstrap
  ./configure --prefix=/usr
  make
}

package()
{
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  cp mit-license.txt "$pkgdir"/usr/share/licenses/$pkgname
}
