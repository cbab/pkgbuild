# Maintainer: Christian Babeux <christian.babeux@0x80.ca> 
pkgname=sysdig
pkgver=0.1.79
pkgrel=2
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('i686' 'x86_64')
url="http://www.sysdig.org/"
license=('GPL2')
depends=('linux-headers' 'dkms')
makedepends=('git' 'cmake')
source=("https://github.com/draios/sysdig/archive/$pkgver.tar.gz")
sha1sums=('ce1bab42030bbaef240c89290bc3668ffe3a63e3')
install=${pkgname}.install

build()
{
    cd ${srcdir}/${pkgname}-${pkgver}
    rm -rf build
    mkdir build
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr -DSYSDIG_VERSION=${pkgver} ..
    make
}

package()
{
    cd ${srcdir}/${pkgname}-${pkgver}
    cd build
    make install DESTDIR=${pkgdir}
}
