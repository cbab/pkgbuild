# Maintainer: Christian Babeux <christian.babeux@0x80.ca>

pkgname=lttng-ust-git
pkgver=20130124
pkgrel=1
pkgdesc="Userspace tracing library for LTTng"
arch=('i686' 'x86_64')
url="http://lttng.org/"
license=('GPL2 LGPL2')
depends=('liburcu>=0.7.2' 'util-linux')
makedepends=('git')
provides=('lttng-ust')
conflicts=('lttng-ust')
optdepends=('babeltrace')
install='lttng-ust-git.install'

_gitroot="git://git.lttng.org/lttng-ust.git"
_gitname="lttng-ust"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  ./bootstrap
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  make DESTDIR="$pkgdir/" install
}
